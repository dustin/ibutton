dnl $Id: configure.in,v 1.8 2002/01/23 23:56:01 dustin Exp $
dnl Process this file with autoconf to produce a configure script.
AC_INIT(mlan.c)

dnl Checks for programs.
AC_PROG_CC(-g)
AC_PROG_RANLIB
AC_PATH_PROG(AR, ar, /bin/ar, $PATH)
AC_PATH_PROG(LINT, lint, /bin/lint, $PATH)
AC_PROGRAM_CHECK(UNAMEPROG, uname, uname, )
AC_PROGRAM_CHECK(TRPROG, tr, tr, )
AC_PROGRAM_CHECK(SEDPROG, sed, sed, )
AC_PROGRAM_CHECK(CUTPROG, cut, cut, )
AC_PROGRAM_CHECK(GUILECONF, guile-config, guile-config, )

dnl Do we do dynamically loading libs on this OS?
DYNLIBS=0
NEEDUNDERSCORE=0
MAKESO=:

dnl I needed these for something, might as well not include them if they
dnl don't exist.
if test -d /usr/local/include
then
	CPPFLAGS="$CPPFLAGS -I/usr/local/include"
fi
if test -d /usr/local/lib
then
	LDFLAGS="$LDFLAGS -L/usr/local/lib"
fi

dnl What we on, boys?
echo "Checking out your OS..."
OS=`$UNAMEPROG|$TRPROG '[[A-Z]]' '[[a-z]]'`
VER=`$UNAMEPROG -r`
if test "$OS" = "sunos" && test "$VER" -ge 5
then
    OS="solaris"
fi
case "$OS" in
    'sunos')
        echo "Woohoo!  SunOS 4.x, one of my favs"
	if test "$GCC" = "yes"
	then
	    echo "Using GCC, let's try to make shared libs."
	    DYNLIBS=1
	    NEEDUNDERSCORE=0
	    MAKESO="$CC -shared"
	    CFLAGS="$CFLAGS -fPIC"
	fi
	;;
    'netbsd')
	echo "NetBSD!  One of my favs."
	;;
    'solaris')
	echo "Solaris?!?  Oh, alright..."
	;;
    'hp-ux')
        echo "HP-UX, this should be fun..."
        if test "$GCC" != "yes"
        then
            echo "Setting ANSI flags"
            CFLAGS="$CFLAGS -Ae"
        fi
        BSDLIKE=0
        ;;
    'irix')
	echo "Looks like IRIX it's going to be..."
	if test "$GCC" = "yes"
	then
	    echo "That's odd, we're using gcc...  OK."
	    echo "If you have the IDO, please set the environ CC to cc"
	    echo "to use the native compiler."
	else
	    echo "Using the native IDO, let's set some CFLAGS"
	    CFLAGS="$CFLAGS -fullwarn"
	    DYNLIBS=1
	    NEEDUNDERSCORE=0
	    MAKESO="ld -shared"
	fi
	;;
esac

if test "$GCC" = "yes"
then
	echo "GCC, setting -Wall and -Werror"
	CPPFLAGS="$CPPFLAGS -Wall -Werror"
fi

dnl If there's guile, build the guile stuff.
if test "$GUILECONF" = ""
then
	GUILESTUFF=""
	GUILEINC=""
else
	GUILESTUFF="mlanscm"
	GUILEINC=`$GUILECONF compile`
fi
AC_SUBST(GUILESTUFF)
AC_SUBST(GUILEINC)

AC_DEFINE_UNQUOTED(DYNLIBS, $DYNLIBS)
AC_SUBST(DYNLIBS)

AC_DEFINE_UNQUOTED(NEEDUNDERSCORE, $NEEDUNDERSCORE)
AC_SUBST(NEEDUNDERSCORE)

AC_DEFINE_UNQUOTED(MAKESO, $MAKESO)
AC_SUBST(MAKESO)

if test "$OS" = "unixware"
then
    AC_CHECK_LIB(ucb, main)
    AC_CHECK_LIB(gen, main)
fi

dnl Solaris stuff
AC_CHECK_LIB(socket, socket)
AC_CHECK_LIB(nsl, gethostbyname)
RRDLIB=""
AC_CHECK_LIB(rrd, rrd_update,
	[RRDLIB="-lrrd"
	AC_DEFINE_UNQUOTED(RRDLIB, $RRDLIB)])
AC_SUBST(RRDLIB)

dnl Checks for header files.
AC_HEADER_DIRENT
AC_HEADER_STDC
AC_HEADER_SYS_WAIT
AC_HEADER_TIME
AC_CHECK_HEADERS(sys/time.h sys/errno.h unistd.h getopt.h getopts.h)
AC_CHECK_HEADERS(rrd.h)

dnl Checks for typedefs, structures, and compiler characteristics.
AC_STRUCT_TM
AC_C_BIGENDIAN

dnl Checks for library functions.
AC_TYPE_SIGNAL
AC_CHECK_FUNCS(getopt vsprintf vsnprintf snprintf sprintf strerror)

CFLAGS="$CFLAGS $CPPFLAGS"

AC_OUTPUT(Makefile collection/Makefile)
